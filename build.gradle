/*
 * Copyright (C) 2015 The Simlar Authors.
 *
 * This file is part of Simlar. (https://www.simlar.org)
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301, USA.
 *
 */
//file:noinspection UnnecessaryQualifiedReference

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "war"
    id "pmd"
    id "com.github.spotbugs" version "6.0.8"
    id "org.springframework.boot" version "3.2.3"
    id "io.spring.dependency-management" version "1.1.4"
    id "com.github.ben-manes.versions" version "0.50.0"
    id "org.owasp.dependencycheck" version "9.0.7"
}

ext {
    lombokVersion = "1.18.30"
    spotbugsVersion = "4.8.3"
}

ext['log4j2.version'] = '2.17.1'

dependencies {
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testCompileOnly("org.projectlombok:lombok:${lombokVersion}")

    compileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}")
    testCompileOnly("com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}")
    spotbugs("com.github.spotbugs:spotbugs:${spotbugsVersion}")
    spotbugsPlugins("com.mebigfatguy.sb-contrib:sb-contrib:7.6.4") { transitive = false }
    spotbugsPlugins("com.h3xstream.findsecbugs:findsecbugs-plugin:1.12.0")

    //noinspection GradlePackageVersionRange, GradlePackageUpdate /// managed by spring boot bom
    implementation("org.springframework.boot:spring-boot-starter-web-services")
    //noinspection GradlePackageVersionRange, GradlePackageUpdate /// managed by spring boot bom
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.apache.commons:commons-lang3:3.14.0")
    implementation("org.apache.commons:commons-text:1.11.0")
    implementation("org.apache.commons:commons-collections4:4.4")
    implementation("commons-codec:commons-codec:1.16.1")
    implementation("com.googlecode.libphonenumber:libphonenumber:8.13.27")
    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.google.auth:google-auth-library-oauth2-http:1.20.0")
    implementation("org.yaml:snakeyaml:2.2") /// TODO: remove this once transitive dependency passes owasp checker

    runtimeOnly("org.mariadb.jdbc:mariadb-java-client:2.7.11")
    //noinspection GradlePackageVersionRange, GradlePackageUpdate /// managed by spring boot bom
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")

    testImplementation("junit:junit:4.13.2")
    //noinspection VulnerableLibrariesLocal
    testImplementation("com.h2database:h2:1.4.200")
    //noinspection GradlePackageVersionRange, GradlePackageUpdate /// managed by spring boot bom
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    //noinspection VulnerableLibrariesLocal
    testImplementation("org.mock-server:mockserver-netty:5.15.0")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

pmd {
    toolVersion = "6.55.0"
    ruleSetFiles = files("pmd-ruleset.xml")
    ruleSets = []
}

spotbugs {
    effort = com.github.spotbugs.snom.Effort.MAX
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf("LOW")
    // implemented like the documentation suggests:
    //   https://github.com/spotbugs/spotbugs-gradle-plugin#readme
    //noinspection GroovyAssignabilityCheck,GrFinalVariableAccess
    excludeFilter = file("$rootProject.projectDir/ides/spotbugs/excludeFilter.xml")
}

gradle.taskGraph.beforeTask { task ->
    if (task.name.toLowerCase().contains('spotbugs')) {
        task.reports {
            html.enabled = true
            xml.enabled = false
        }
    }
}

dependencyCheck {
    suppressionFile = "owasp-dependency-check-suppression.xml"
    failBuildOnCVSS = 0
    def nvdApiKey = System.getenv("SIMLAR_NVD_API_KEY")
    if (nvdApiKey != null) {
        println("This product uses the NVD API but is not endorsed or certified by the NVD.")
        nvd {
            apiKey = "${nvdApiKey}"
            delay = 16000
        }
    }
}

//noinspection GroovyAssignabilityCheck
tasks.register('dependencyChecks') {
}
dependencyChecks.dependsOn dependencyCheckAnalyze
dependencyChecks.dependsOn dependencyUpdates

processResources {
    filesMatching("**/application.properties") {
        expand(project.properties)
    }
}

//noinspection GroovyMissingReturnStatement
test {
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
}

def getGitVersion = { ->
    try {
        return providers.exec {
            commandLine("git", "describe", "--tags", "--always", "--dirty")
        }.standardOutput.asText.get().trim()
    }
    catch (ignored) {
        return "git-not-found"
    }
}

version = getGitVersion()

tasks.named("war") {
    enabled = true
    archiveFileName = "simlar-server##${project.version}.war"
    rootSpec.exclude("application-default.properties")
    manifest {
        attributes("Implementation-Version": project.version)
    }
}

tasks.named("bootWar") {
    rootSpec.exclude("application-default.properties")
    manifest {
        attributes("Implementation-Version": project.version)
    }
}

tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

springBoot {
    buildInfo {
        properties {
            excludes = ['time']
        }
    }
}

idea {
    module {
        /// IntelliJ inspection excludes
        excludeDirs += file("ides")
        excludeDirs += file("examples")
        excludeDirs += file("pmd-ruleset.xml")
        excludeDirs += file("owasp-dependency-check-suppression.xml")
        excludeDirs += file("gradlew.bat")
        excludeDirs += file("gradlew")
    }
}
